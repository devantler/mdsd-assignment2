/*
 * generated by Xtext 2.25.0
 */
package dk.sdu.mmmi.mdsd.generator

import dk.sdu.mmmi.mdsd.math.Model
import dk.sdu.mmmi.mdsd.math.Number
import java.util.HashMap
import java.util.Map
import javax.swing.JOptionPane
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import dk.sdu.mmmi.mdsd.math.DivisionExpression
import dk.sdu.mmmi.mdsd.math.MultiplicationExpression
import dk.sdu.mmmi.mdsd.math.AdditionExpression
import dk.sdu.mmmi.mdsd.math.SubtractionExpression
import dk.sdu.mmmi.mdsd.math.VariableReference
import dk.sdu.mmmi.mdsd.math.LocalVariable
import dk.sdu.mmmi.mdsd.math.Variable

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class MathGenerator extends AbstractGenerator {

	static Map<String, Integer> variables

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val model = resource.allContents.filter(Model).next
		val result = model.compute

		result.displayPanel
	}

	def static compute(Model model) {
		variables = new HashMap()
		for (variable : model.variables) {
			val localVariables = new HashMap<String, Integer>();
			variables.put(variable.name, variable.expression.computeExp(localVariables))
		}
		return variables
	}

	def dispatch static int computeExp(AdditionExpression expression, Map<String, Integer> localVariables) {
		expression.left.computeExp(localVariables) + expression.right.computeExp(localVariables)
	}

	def dispatch static int computeExp(SubtractionExpression expression, Map<String, Integer> localVariables) {
		expression.left.computeExp(localVariables) - expression.right.computeExp(localVariables)
	}

	def dispatch static int computeExp(MultiplicationExpression expression, Map<String, Integer> localVariables) {
		expression.left.computeExp(localVariables) * expression.right.computeExp(localVariables)
	}

	def dispatch static int computeExp(DivisionExpression expression, Map<String, Integer> localVariables) {
		expression.left.computeExp(localVariables) / expression.right.computeExp(localVariables)
	}

	def dispatch static int computeExp(Number number, Map<String, Integer> localVariables) {
		number.value
	}
	
	def dispatch static int computeExp(Variable variable, Map<String, Integer> localVariables){
		val nestedVariables = new HashMap(localVariables);
		if(variable instanceof LocalVariable){
			nestedVariables.put(variable.name, variable.local_expression.computeExp(nestedVariables))
		}
		variable.expression.computeExp(nestedVariables)
	}

	def dispatch static int computeExp(VariableReference reference, Map<String, Integer> localVariables) {
		val globalVariable = variables.get(reference.variable.name)
		val localVariable = localVariables.get(reference.variable.name)
		if (reference.variable instanceof LocalVariable) {
			return localVariable !== null ? localVariable : globalVariable
		} else {
			return globalVariable !== null ? globalVariable : reference.variable.computeExp(localVariables)
		}
	}

	def void displayPanel(Map<String, Integer> result) {
		var resultString = ""
		for (entry : result.entrySet()) {
			resultString += "var " + entry.getKey() + " = " + entry.getValue() + "\n"
		}

		JOptionPane.showMessageDialog(null, resultString, "Math Language", JOptionPane.INFORMATION_MESSAGE)
	}

}
