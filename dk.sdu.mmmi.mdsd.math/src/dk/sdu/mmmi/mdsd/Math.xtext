grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Model:
	variables+=Variable*;

Variable:
	'var' name=ID '=' expression=Addition;

Addition returns Expression:
	Subtraction ({Addition.left=current} '+' right=Subtraction)*;

Subtraction returns Expression:
	Multiplication ({Subtraction.left=current} '-' right=Multiplication)*;

Multiplication returns Expression:
	Division ({Multiplication.left=current} '*' right=Division)*;

Division returns Expression:
	Value ({Division.left=current} '/' right=Value)*;

Value returns Expression:
	Parenthesis | Number | LocalVariable | Reference;

Parenthesis returns Expression:
	'(' Addition ')';

Number:
	value=INT;

LocalVariable:
	'let' name=ID '=' binding=Addition 'in' body=Addition 'end';

Reference:
	variable=[Variable];